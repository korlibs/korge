plugins {
    id 'kotlin-multiplatform' version '1.3.11'
    id 'com.moowork.node' version '1.2.0'
}

def dump(obj) {
    println("${obj.class}: $obj")
    obj.properties.each { println " - $it.key : ${it.value.class.name}   ->   $it.value" }
}

allprojects {
    repositories {
        mavenLocal()
        maven { url "https://dl.bintray.com/soywiz/soywiz" }
        jcenter()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'kotlin-multiplatform'
    apply plugin: 'com.moowork.node'

    kotlin {
        targets {
            //fromPreset(presets.iosX64, 'iosX64')
            //fromPreset(presets.iosArm32, 'iosArm32')
            //fromPreset(presets.iosArm64, 'iosArm64')
            fromPreset(presets.macosX64, 'macosX64')
            //fromPreset(presets.linuxX64, 'linuxX64')
            fromPreset(presets.mingwX64, 'mingwX64')
            fromPreset(presets.jvm, 'jvm')
            fromPreset(presets.js, 'js') {
                compilations.main {
                    compileKotlinJs.kotlinOptions {
                        languageVersion = "1.3"
                        sourceMap = true
                        metaInfo = true
                        moduleKind = "umd"
                    }
                    compileTestKotlinJs.kotlinOptions {
                        languageVersion = "1.3"
                        moduleKind = "umd"
                    }
                }
            }
        }
        sourceSets {
            nonNativeCommonMain
            nativeCommonMain
            nativeCommonTest
            nativePosixMain
            configure([jvmMain, jsMain]) {
                dependsOn nonNativeCommonMain
            }
            mingwX64Main {
                dependsOn nativeCommonMain
            }
            mingwX64Test {
                dependsOn nativeCommonTest
            }
            configure([iosX64Main, iosArm32Main, iosArm64Main, macosX64Main, linuxX64Main]) {
                dependsOn nativeCommonMain
                dependsOn nativePosixMain
            }
            configure([iosX64Test, iosArm32Test, iosArm64Test, macosX64Test, linuxX64Test]) {
                dependsOn nativeCommonTest
            }
        }
    }

    dependencies {
        commonMainImplementation "org.jetbrains.kotlin:kotlin-stdlib-common"
        commonTestImplementation "org.jetbrains.kotlin:kotlin-test-annotations-common"
        commonTestImplementation "org.jetbrains.kotlin:kotlin-test-common"

        jsMainImplementation "org.jetbrains.kotlin:kotlin-stdlib-js"
        jsTestImplementation "org.jetbrains.kotlin:kotlin-test-js"

        jvmMainImplementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
        jvmTestImplementation "org.jetbrains.kotlin:kotlin-test"
        jvmTestImplementation "org.jetbrains.kotlin:kotlin-test-junit"
    }

    // Javascript test configuration

    def korlibsDir = new File(System.getProperty("user.home"), ".korlibs")
    korlibsDir.mkdirs()

    node {
        version = '8.11.4'
        download = true
        workDir = new File(korlibsDir, "nodejs")
        npmWorkDir = new File(korlibsDir, "npm")
        yarnWorkDir = new File(korlibsDir, "yarn")
        nodeModulesDir = new File(korlibsDir, "node_modules")
    }

    def jsCompilations = kotlin.targets.js.compilations
    task installMocha(type: NpmTask) {
        onlyIf { !(new File(node.nodeModulesDir, "mocha")).exists() }
        args = ["install", "mocha@5.2.0"]
    }
    task installJsCanvas(type: NpmTask) {
        onlyIf { !(new File(node.nodeModulesDir, "canvas")).exists() }
        args = ["install", "canvas@2.2.0"]	    //dump(kotlin.sourceSets.commonTest.resources)
    }
    //dump(kotlin.sourceSets.commonTest.resources)

    task populateNodeModules {
        doLast {
            copy {
                from "${node.nodeModulesDir}"
                from jsCompilations.main.output.allOutputs
                from jsCompilations.test.output.allOutputs
                jsCompilations.test.runtimeDependencyFiles.each {
                    if (it.exists() && !it.isDirectory()) {
                        from zipTree(it.absolutePath).matching { include '*.js' }
                    }
                }
                for (sourceSet in kotlin.sourceSets) {
                    from sourceSet.resources
                }

                into "$buildDir/node_modules"
            }
        }
    }

    task runMocha(type: NodeTask, dependsOn: [jsCompilations.test.compileKotlinTaskName, installMocha, populateNodeModules, installJsCanvas]) {
        script = file("$buildDir/node_modules/mocha/bin/mocha")
        workingDir = file("$buildDir/node_modules")
        args = ["--timeout", "15000", "${project.name}_test.js"]
    }

    task jsInstallMochaHeadlessChrome(type: NpmTask) {
        onlyIf { !(new File("${node.nodeModulesDir}/mocha-headless-chrome")).exists() }
        args = ["install", "mocha-headless-chrome@2.0.1"]
    }

    task jsTestChrome(type: NodeTask, dependsOn: [jsCompilations.test.compileKotlinTaskName, jsInstallMochaHeadlessChrome, installMocha, populateNodeModules]) {
        doFirst {
            new File("$buildDir/node_modules/tests.html").write("""
                <!DOCTYPE html>
                <html>
                <head>
                    <title>Mocha Tests</title>
                    <meta charset="utf-8">
                    <link rel="stylesheet" href="mocha/mocha.css">
                    <script src="https://cdnjs.cloudflare.com/ajax/libs/require.js/2.3.6/require.min.js"></script>
                </head>
                <body>
                <div id="mocha"></div>
                <script src="mocha/mocha.js"></script>
                <script>
                    requirejs.config({'baseUrl': '.', 'paths': { 'tests': '../classes/kotlin/js/test/${project.name}_test' }});
                    mocha.setup('bdd');
                    require(['tests'], function() { mocha.run(); });
                </script>
                </body>
                </html>
            """)
        }
        script = file("${node.nodeModulesDir}/mocha-headless-chrome/bin/start")
        args = ['-f', "$buildDir/node_modules/tests.html", "-a", "no-sandbox", "-a", "disable-setuid-sandbox", "-a", "allow-file-access-from-files"]
    }

    afterEvaluate {
        //for (target in ["macosX64", "linuxX64", "mingwX64"]) {
        for (target in ["macosX64", "mingwX64"]) {
            String taskName = "copyResourcesToExecutable_${target}"
            def targetTestTask = tasks.getByName("${target}Test")
            tasks.create(name: taskName, type: Copy) {
                for (sourceSet in kotlin.sourceSets) {
                    from sourceSet.resources
                }
                into new File(targetTestTask.inputs.properties.executable).parentFile
            }
            targetTestTask.dependsOn(taskName)
        }
    }

    // Only run JS tests if not in windows
    if (!org.apache.tools.ant.taskdefs.condition.Os.isFamily(org.apache.tools.ant.taskdefs.condition.Os.FAMILY_WINDOWS)) {
        jsTest.dependsOn runMocha
    }

    // Fix for https://github.com/srs/gradle-node-plugin/issues/301
    repositories.whenObjectAdded {
        if (it instanceof IvyArtifactRepository) {
            metadataSources {
                artifact()
            }
        }
    }

    // Publishing
    group 'com.soywiz'
    version projectVersion
    apply plugin: 'maven-publish'

    def pomBaseData = {
        licenses {
            license {
                name project.property("project.license.name")
                url project.property("project.license.url")
            }
        }
        scm {
            url project.property("project.scm.url")
        }
    }

    def generatePom = { pom ->
        pom.withXml {
            def root = it.asNode()
            root.appendNode('name', project.name)
            root.appendNode('description', project.property("project.description"))
            root.appendNode('url', project.property("project.scm.url"))
            root.children().last() + pomBaseData
        }
    }

    ext.generatePom = generatePom

    def publishUser = rootProject.findProperty('BINTRAY_USER') ?: project.findProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
    def publishPassword = rootProject.findProperty('BINTRAY_KEY') ?: project.findProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY')

    if (publishUser && publishPassword) {
        publishing {
            repositories {
                maven {
                    credentials {
                        username publishUser
                        password publishPassword
                    }
                    url "https://api.bintray.com/maven/soywiz/soywiz/${project.property('project.package')}/"
                }
            }

            configure(publications) {
                generatePom(pom)
            }
        }
    }

    if (project.file('build.project.gradle').exists()) {
        apply from: project.file('build.project.gradle')
    }
}
