plugins {
    id "org.gradle.kotlin.kotlin-dsl" version "2.1.7"
    id "publishing"
    id "maven-publish"
    id "signing"
}

def kotlinVersion = libs.versions.kotlin.get()
def androidBuildGradleVersion
if (System.getProperty("java.version").startsWith("1.8") || System.getProperty("java.version").startsWith("9")) {
    androidBuildGradleVersion = "4.2.0"
} else {
    androidBuildGradleVersion = libs.versions.android.build.gradle.get()
}

dependencies {
    implementation("com.android.tools.build:gradle:$androidBuildGradleVersion")
    implementation("org.jetbrains.kotlin:kotlin-gradle-plugin:${libs.versions.kotlin.get()}")
    implementation(libs.proguard.gradle)
    implementation(libs.closure.compiler)
    implementation(libs.gson)
    implementation(libs.kotlinx.compose.gradle.plugin)
}

repositories {
    mavenLocal()
    mavenCentral()
    google()
    if (kotlinVersion.contains("eap") || kotlinVersion.contains("-")) {
        maven { url = uri("https://maven.pkg.jetbrains.space/kotlin/p/kotlin/temporary") }
        maven { url = uri("https://maven.pkg.jetbrains.space/kotlin/p/kotlin/dev") }
        maven { url = uri("https://maven.pkg.jetbrains.space/public/p/kotlinx-coroutines/maven") }
    }
    maven { url = uri("https://oss.sonatype.org/content/repositories/snapshots/") }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.suppressWarnings = true
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
java.sourceSets["main"].java {
    srcDir("src/main/kotlinGen")
}
