plugins {
    id "publishing"
    id "maven-publish"
    id "signing"
    id "org.jetbrains.kotlin.jvm" version libs.versions.kotlin
}

def isJava8or9 = System.getProperty("java.version").startsWith("1.8") || System.getProperty("java.version").startsWith("9")

dependencies {
    implementation(libs.kover)
    implementation(libs.dokka)
    implementation(libs.proguard.gradle)
    implementation(libs.closure.compiler)
    implementation(libs.gson)
    implementation(libs.gradle.publish.plugin)
    implementation(libs.kotlin.gradle.plugin)
    if (isJava8or9) {
        implementation(libs.android.build.gradle.java8)
    } else {
        implementation(libs.android.build.gradle.java11)
    }
}

Eval.xy(this, this, file("../gradle/repositories.settings.gradle").text)

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.suppressWarnings = true
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    sourceSets.main.java {
        srcDir("src/main/kotlinGen")
        if (isJava8or9) {
            srcDir("src/main/kotlin8")
        } else {
            srcDir("src/main/kotlin11")
        }
    }
}

// Build versions

String gitVersion = "unset"

try {
    gitVersion = file("../.git/ORIG_HEAD").text.trim()
} catch (Throwable e) {
}

if (System.getenv("FORCED_VERSION") != null) {
    try {
        gitVersion = Runtime.getRuntime().exec("git describe --abbrev=8 --tags --dirty", new String[0], rootDir).inputStream.text.trim()
    } catch (Throwable e) {
        System.err.println(e.message)
    }
}

Properties props = new Properties()
props.load(new StringReader(file("../gradle.properties").text))

String projectVersion = System.getenv("FORCED_VERSION")?.replaceFirst(~"^refs/tags/", "")?.replaceFirst(~"^v", "")?.replaceFirst(~"^w", "")
    ?: props.version

String realKotlinVersion = System.getenv("FORCED_KOTLIN_VERSION") ?: libs.versions.kotlin.get()

String buildVersionsString = """
package com.soywiz.korge.gradle

object BuildVersions {
    const val GIT = "---"
    const val KOTLIN = "${realKotlinVersion}"
    const val NODE_JS = "${libs.versions.node.get()}"
    const val JNA = "${libs.versions.jna.get()}"
    const val COROUTINES = "${libs.versions.kotlinx.coroutines.get()}"
    const val ANDROID_BUILD = "${isJava8or9 ? libs.versions.android.build.gradle.java8.get() : libs.versions.android.build.gradle.java11.get()}"
    const val KOTLIN_SERIALIZATION = "${libs.versions.kotlinx.serialization.get()}"
    const val KORLIBS = "$projectVersion"
    const val KRYPTO = "$projectVersion"
    const val KLOCK = "$projectVersion"
    const val KDS = "$projectVersion"
    const val KMEM = "$projectVersion"
    const val KORMA = "$projectVersion"
    const val KORIO = "$projectVersion"
    const val KORIM = "$projectVersion"
    const val KORAU = "$projectVersion"
    const val KORGW = "$projectVersion"
    const val KORTE = "$projectVersion"
    const val KORGE = "$projectVersion"

    val ALL_PROPERTIES by lazy { listOf(::GIT, ::KRYPTO, ::KLOCK, ::KDS, ::KMEM, ::KORMA, ::KORIO, ::KORIM, ::KORAU, ::KORGW, ::KORGE, ::KOTLIN, ::JNA, ::COROUTINES, ::ANDROID_BUILD, ::KOTLIN_SERIALIZATION) }
    val ALL by lazy { ALL_PROPERTIES.associate { it.name to it.get() } }
}
""".trim()

String buildVersionsStringForBuildSrc = buildVersionsString
String buildVersionsStringForPlugin = buildVersionsString.replace(
    "const val GIT = \"---\"",
    "const val GIT = \"${gitVersion}\""
)

File buildsVersionBuildSrcFile = file("../buildSrc/src/main/kotlinGen/com/soywiz/korge/gradle/BuildVersions.kt")
File buildsVersionFilePlugin = file("../korge-gradle-plugin/build/srcgen/com/soywiz/korge/gradle/BuildVersions.kt")

if (!buildsVersionBuildSrcFile.exists() || buildsVersionBuildSrcFile.text != buildVersionsStringForBuildSrc) {
    buildsVersionBuildSrcFile.parentFile.mkdirs()
    buildsVersionBuildSrcFile.text = buildVersionsStringForBuildSrc
}
if (!buildsVersionFilePlugin.exists() || buildsVersionFilePlugin.text != buildVersionsStringForPlugin) {
    buildsVersionFilePlugin.parentFile.mkdirs()
    buildsVersionFilePlugin.text = buildVersionsStringForPlugin
}
