buildscript {
    repositories {
        mavenLocal()
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap-1.1" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

group 'com.soywiz'
version "$korVersion"

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    mavenLocal()
    maven { url "https://dl.bintray.com/kotlin/kotlin-eap-1.1" }
    mavenCentral()
}

sourceSets {
    main.java.srcDirs = ['src']
    test.java.srcDirs = ['test']
    main.resources.srcDirs = ['resources']
    test.resources.srcDirs = ['testresources']
    main.java.srcDirs += 'src/main/kotlin'
}

dependencies {
    compile "com.soywiz:korio-core:$korVersion"
    compile "com.soywiz:korim-core:$korVersion"
    compile "com.soywiz:korag-core:$korVersion"
    compile "com.soywiz:korau-core:$korVersion"
    compile "com.soywiz:korui-core:$korVersion"
    //compile "com.soywiz:korte:$korVersion"
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
}

task javadoc2(type: Javadoc) {
    failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc2) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

// gradle uploadArchives
if (project.hasProperty('sonatypeUsername')) {
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: project.sonatypeUsername, password: project.sonatypePassword)
                }

                pom.project {
                    name "${project.name}"
                    packaging 'jar'
                    description 'korge: Kotlin cORoutines Game Engine'
                    url 'https://github.com/soywiz/korge/'
                    inceptionYear '2017'

                    scm {
                        url 'scm:git@github.com:soywiz/korge.git'
                        connection 'scm:git@github.com:soywiz/korge.git'
                        developerConnection 'scm:git@github.com:soywiz/korge.git'
                    }

                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }

                    developers {
                        developer {
                            id 'soywiz'
                            name 'Carlos Ballesteros Velasco'
                        }
                    }
                }
            }
        }
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId project.group
            artifactId project.name
            version "$project.version"
        }
    }
}

// gradle bintrayUpload
if (project.hasProperty('BINTRAY_USER')) {
    bintray {
        // https://github.com/bintray/gradle-bintray-plugin#buildgradle
        //Properties bintray = new Properties()
        //bintray.load(new FileInputStream(System.getProperty("user.home") + "/.bintray.properties"))

        user = BINTRAY_USER
        key = BINTRAY_KEY
        publications = ['MyPublication']
        publish = true
        override = true

        pkg {
            repo = "soywiz-maven"
            name = project.name
            userOrg = 'soywiz'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/soywiz/korge.git'
        }
    }
}

task deploy(dependsOn: ['install', 'bintrayUpload', 'uploadArchives']) {
}
