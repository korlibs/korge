plugins {
    id "java"
    id "java-gradle-plugin"
    id "maven-publish"
    id "com.gradle.plugin-publish"
    id "org.jetbrains.kotlin.jvm"
}

description = "Multiplatform Game Engine written in Kotlin"
group = "com.soywiz.korlibs.korge.plugins"
//group = "korlibs.korge"
//this.name = "korlibs.korge.gradle.plugin"

gradlePlugin {
    website = "https://korge.soywiz.com/"
    vcsUrl = "https://github.com/korlibs/korge-plugins"
    //tags = ["korge", "game", "engine", "game engine", "multiplatform", "kotlin"]

	plugins {
		create("korge") {
            //PluginDeclaration decl = it
			//id = "korlibs.korge"
            id = "com.soywiz.korge"
			displayName = "Korge"
			description = "Multiplatform Game Engine for Kotlin"
			implementationClass = "korlibs.korge.gradle.KorgeGradlePlugin"
		}
	}
}

/*
afterEvaluate {
    GenerateMavenPom generatePomFileForKorgePluginMarkerMavenPublication = tasks.findByName("generatePomFileForKorgePluginMarkerMavenPublication")
    generatePomFileForKorgePluginMarkerMavenPublication.pom.licenses {
        it.license {
            it.name = "MIT"
        }
    }
}
*/

def jversion = korlibs.korge.gradle.targets.android.AndroidKt.GRADLE_JAVA_VERSION_STR

java {
    sourceCompatibility = jversion
    targetCompatibility = jversion
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = jversion
        sourceCompatibility = jversion
        apiVersion = "1.7"
        languageVersion = "1.7"
		//jvmTarget = "1.6"
    }
}

kotlin.sourceSets.main.each {
    it.kotlin.srcDirs(new File(buildDir, "srcgen"), new File(buildDir, "srcgen2"))
}
java.sourceSets.main.each {
    it.resources.srcDirs(new File(buildDir, "srcgen2res"))
}

korlibs.NativeTools.groovyConfigurePublishing(project, false)
korlibs.NativeTools.groovyConfigureSigning(project)

dependencies {
    implementation(libs.kotlin.gradle.plugin)
    implementation(libs.kotlin.serialization)

    implementation(libs.proguard.gradle)
    implementation(libs.closure.compiler)
    implementation(libs.gson)
    implementation(libs.gradle.publish.plugin)

    implementation(libs.kover)
    implementation(libs.dokka)

    implementation(libs.android.build.gradle)

    implementation(gradleApi())
	implementation(localGroovy())
    //compileOnly(gradleKotlinDsl())

    testImplementation("org.jetbrains.kotlin:kotlin-test")
    testImplementation("org.jetbrains.kotlin:kotlin-test-junit")
    testImplementation("io.mockk:mockk:1.11.0")

    //implementation(project(":korge-reload-agent"))
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions.suppressWarnings = true
}

//def publishAllPublications = false

def publishJvmPublicationToMavenLocal = tasks.register("publishJvmPublicationToMavenLocal", Task) {
    group = "publishing"
    //dependsOn(publishAllPublications ? "publishToMavenLocal" : "publishPluginMavenPublicationToMavenLocal")
    dependsOn("publishPluginMavenPublicationToMavenLocal")
    dependsOn("publishKorgePluginMarkerMavenPublicationToMavenLocal")
}

// publishKorgePluginMarkerMavenPublicationToMavenLocal

afterEvaluate {
    //def publishTaskOrNull = tasks.findByName(publishAllPublications ? "publishAllPublicationsToMavenRepository" : "publishPluginMavenPublicationToMavenRepository")

    if (tasks.findByName("publishKorgePluginMarkerMavenPublicationToMavenRepository") != null) {
        def publishJvmPublicationToMavenRepository = tasks.register("publishJvmPublicationToMavenRepository", Task) {
            group = "publishing"
            dependsOn("publishPluginMavenPublicationToMavenRepository")
            dependsOn("publishKorgePluginMarkerMavenPublicationToMavenRepository")
        }
    }
}

def jvmTest = tasks.register("jvmTest", Task) {
    dependsOn("test")
}
