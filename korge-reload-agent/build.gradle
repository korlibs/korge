plugins {
    //id "kotlin" version "1.6.21"
    id "kotlin"
    //id "org.jetbrains.kotlin.jvm"
    id "maven-publish"
}

description = "Multiplatform Game Engine written in Kotlin"
group = "com.soywiz.korlibs.korge.reloadagent"

jar {
    manifest {
        attributes(
            "Agent-Class": "korlibs.korge.reloadagent.KorgeReloadAgent",
            "Premain-Class": "korlibs.korge.reloadagent.KorgeReloadAgent",
            "Can-Redefine-Classes": true,
            "Can-Retransform-Classes": true
        )
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
        sourceCompatibility = "1.8"
        apiVersion = "1.6"
        languageVersion = "1.6"
        suppressWarnings = true
        //jvmTarget = "1.6"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'korge-reload-agent'
            version = version
            from components.kotlin
        }
    }
}

def publishJvmPublicationToMavenLocal = tasks.register("publishJvmPublicationToMavenLocal", Task) {
    group = "publishing"
    dependsOn("publishMavenPublicationToMavenLocal")
}

afterEvaluate {
    if (tasks.findByName("publishMavenPublicationToMavenRepository") != null) {
        tasks.register("publishJvmPublicationToMavenRepository", Task) {
            group = "publishing"
            dependsOn("publishMavenPublicationToMavenRepository")
        }
    }
}

korlibs.NativeTools.groovyConfigurePublishing(project, false)
korlibs.NativeTools.groovyConfigureSigning(project)