plugins {
    id "kotlin"
    //id "org.jetbrains.kotlin.jvm"
    id "maven-publish"
}

description = "Multiplatform Game Engine written in Kotlin"
group = "com.soywiz.korlibs.korge.reloadagent"

jar {
    manifest {
        attributes(
            "Agent-Class": "com.soywiz.korge.reloadagent.KorgeReloadAgent",
            "Premain-Class": "com.soywiz.korge.reloadagent.KorgeReloadAgent",
            "Can-Redefine-Classes": true,
            "Can-Retransform-Classes": true
        )
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
        sourceCompatibility = "1.8"
        apiVersion = "1.4"
        languageVersion = "1.4"
        suppressWarnings = true
        //jvmTarget = "1.6"
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = 'korge-reload-agent'
            version = version
            from components.kotlin
        }
    }
}

def publishJvmPublicationToMavenLocal = tasks.register("publishJvmPublicationToMavenLocal", Task) {
    group = "publishing"
    dependsOn("publishMavenPublicationToMavenLocal")
}

afterEvaluate {
    if (tasks.findByName("publishMavenPublicationToMavenRepository") != null) {
        tasks.register("publishJvmPublicationToMavenRepository", Task) {
            group = "publishing"
            dependsOn("publishMavenPublicationToMavenRepository")
        }
    }
}

com.soywiz.korlibs.NativeTools.groovyConfigurePublishing(project, false)
com.soywiz.korlibs.NativeTools.groovyConfigureSigning(project)
